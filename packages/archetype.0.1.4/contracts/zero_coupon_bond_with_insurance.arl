archetype zero_coupon_bond_with_insurance

variable issuer role = @tz1KksC8RvjUWAbXYJuNrUbontHGor25Cztk (* seller ‘Alice’ *)

variable owner role  = @tz1KmuhR6P52hw6xs5P69BXJYAURaznhvN1k
(* buyer ‘Bob’; receives 11 tez in one-year *)

contract insurance = {
   action pay : address, tez
}

instance zero_insr of insurance = @KT1GabhR5P52hw6xs5P69BXJYAURaznhvN1k

variable price tez from owner = 10

variable payment tez from issuer to owner = 11 * price

variable maturity date

states =
 | Created initial
 | Insured   (* Guarantee Fund has accepted issuer *)
 | Confirmed (* owner has purchased bond *)
 | Repaid    (* issuer has transferred payment to contract *)
 | Collected (* owner has collected payment *)

transition insured from Created = {
  called by insurance

  to Insured
}

transition confirm from Insured = {
  specification {
    postcondition s1 = {
      balance = 0
    }
  }

  to Confirmed
  when { transferred = price }
  with effect {
    maturity := now + 365d;
    transfer price to issuer
  }
}

transition repay from Confirmed = {
  called by issuer

  to Repaid
  when {
    transferred = payment
  }
}

transition collect from Repaid = {
  called by owner

  to Collected
  when { now > maturity }
  with effect {
    if balance >= payment
    then transfer balance to owner
    else guarantee_fund.pay (owner, payment)
  }
}
